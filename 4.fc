{-

  TASK 4 - (De)Serialize to Cell
  Write the methods
    a) for serialization of tuple of arbitrary values of different types to Cell
    b) for deserialization of Cell created by method above to original tuple

  `serialize` method gets as input tuple with arbitrary number of elements from 0
  to 128 (both 0 and 128 elements are allowed) and outputs Cell. Elements of the
  tuple may be `null`, `int`, `cell`, `slice`, `tuple` (with the same limitations
  as original tuple). It is guaranteed that the maximum nesting level of tuples
  is less than 4 and total number of elements less than 1024.
  `deserialize` method gets a cell produced by `serialize` method and should
  return origin tuple.

  Note, for illustrative purposes`serialize_t3` and `deserialize_t3` functions
  which serialize and deserialize tuple with exactly 3 elements
  (only null, int, cell and slice are supportd) to/from a cell  have been added.
  Participants are free to not use logic from there and come up with their own
  implementations.

  Tests will only check that test_tuple == deserialize(serialize(test_tuple)).
  Intermediate representation can be arbitrary.
-}


() recv_internal() {
}

(cell) serialize_element(var x) {
    builder data = begin_cell();

    ;; meta: 2 bits
    ;; 0 - value
    ;; 1 - tpl
    ;; 2 - tail
    data = data.store_uint(0, 2);

    ;; type: 2 bits
    ;; 0 - null
    ;; 1 - int
    ;; 2 - cell
    ;; 3 - slice

    if (is_null(x)) {
        data = data.store_uint(0, 2);
    }

    if (is_int(x)) {
        int val = force_cast_to_int(x);
        cell c = begin_cell().store_int(val, 257).end_cell();
        data = data.store_uint(1, 2).store_ref(c);
    }

    if (is_cell(x)) {
        cell c = force_cast_to_cell(x);
        data = data.store_uint(2, 2).store_ref(c);
    }

    if (is_slice(x)) {
        slice s = force_cast_to_slice(x);
        cell c = begin_cell().store_slice(s).end_cell();
        data = data.store_uint(3, 2).store_ref(c);
    }

    return data.end_cell();
}

(cell) serialize_tuple(tuple t, int is_tail, int idx_offset) {
    builder b = begin_cell();

    ;; meta: 2 bits
    ;; 0 - value
    ;; 1 - tpl
    ;; 2 - tail

    if (is_tail) {
        b = b.store_uint(2, 2);
    } else {
        b = b.store_uint(1, 2);
    }

    int cnt = 0;
    int idx = idx_offset + cnt;
    int len = tuple_length(t);
    while ((idx < len) & (cnt < 3)) {
        var x = t.at(idx);

        if (~ is_tuple(x)) {
            cell data = serialize_element(x);
            b = b.store_ref(data);
        } else {
            cell data = serialize_tuple(x, 0, 0);
            b = b.store_ref(data);
        }

        cnt += 1;
        idx += 1;
    }

    if (idx < len) {
        cell data = serialize_tuple(t, 1, idx);
        b = b.store_ref(data);
    }

    return b.end_cell();
}

;; testable
(cell) serialize(tuple values) method_id {
    return serialize_tuple(values, 0, 0);
}

(tuple) deserialize_element(tuple t, slice data) {
    int kind = data~load_uint(2);

    if (kind == 0) {
        t~tpush(null());
    }

    if (kind == 1) {
        cell c = data~load_ref();
        slice s = c.begin_parse();
        int x = s~load_int(257);
        t~tpush(x);
    }

    if (kind == 2) {
        cell c = data~load_ref();
        t~tpush(c);
    }

    if (kind == 3) {
        slice s = data~load_ref().begin_parse();
        t~tpush(s);
    }

    return t;
}

(tuple) deserialize_tuple(tuple result, int new_tuple, slice data) {
    if (new_tuple) {
        result = empty_tuple();
    }

    while (slice_refs(data) > 0) {
        cell c = data~load_ref();
        slice s = c.begin_parse();
        int tag = s~load_uint(2);

        if (tag == 0) {
            result = deserialize_element(result, s);
        }
        if (tag == 1) {
            tuple t = deserialize_tuple(result, 1, s);
            result~tpush(t);
        }
        if (tag == 2) {
            result = deserialize_tuple(result, 0, s);
        }
    }

    return result;
}

;; testable
(tuple) deserialize(cell serialized) method_id {
    return deserialize_tuple(empty_tuple(), 0, serialized.begin_parse());
}

;; ==== Illustrative material ====
;; builder serialize_element(builder b, var x) {
;;     if(is_null(x)) {
;;         b~store_uint(0,3);
;;     }
;;     if(is_int(x)) {
;;         ;; before this point compiler do not know true type of x
;;         ;; force it to be int
;;         int i_x = force_cast_to_int(x);
;;             b = b.store_uint(1,3).store_int(i_x, 257);
;;     }
;;     if(is_cell(x)) {
;;         ;; before this point compiler do not know true type of x
;;         ;; force it to be cell
;;         cell c_x = force_cast_to_cell(x);
;;         b = b.store_uint(2,3).store_ref(c_x);
;;     }
;;     if(is_slice(x)) {
;;         ;; before this point compiler do not know true type of x
;;         ;; force it to be slice
;;         slice s_x = force_cast_to_slice(x);
;;         b = b.store_uint(3,3).store_ref(begin_cell().store_slice(s_x).end_cell());
;;     }
;;     return b;
;; }
;;
;; (slice, tuple) deserialize_element_to_tuple(slice s, tuple t) {
;;     int element_type = s~load_uint(3);
;;     if(element_type == 0) {
;;         t~tpush(null());
;;     }
;;     if(element_type == 1) {
;;         t~tpush(s~load_int(257));
;;     }
;;     if(element_type == 2) {
;;         t~tpush(s~load_ref());
;;     }
;;     if(element_type == 3) {
;;         t~tpush(s~load_ref().begin_parse());
;;     }
;;     return (s,t);
;; }
;;
;; cell serialize_t3(tuple three_elements) method_id {
;;     throw_unless(777, three_elements.tuple_length() == 3);
;;     builder srl = begin_cell();
;;     srl = serialize_element(srl, three_elements.first());
;;     srl = serialize_element(srl, three_elements.second());
;;     srl = serialize_element(srl, three_elements.third());
;;     return srl.end_cell();
;; }
;;
;; tuple deserialize_t3(cell sc) method_id {
;;     slice s = sc.begin_parse();
;;     tuple t = empty_tuple();
;;     repeat (3) {
;;         (s,t) = deserialize_element_to_tuple(s,t);
;;     }
;;     return t;
;; }
